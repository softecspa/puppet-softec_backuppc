# = Class softec_backuppc::client
#
# wrapper of backuppc::client. Call backuppc::client with customized parameter for softec environment.
# Also creates a test file under /etc/restoretest
#
# == Params
#
# [*user*]
#   local user on client machine used by server to connect to. Default: softecbackuppc
#
# [*user_uid*]
#   user uid. Default: 3005
#
# [*user_gid*]
#   user gid. Default: 3005
#
# [*home*]
#   user home. Default: /var/lib/$user
#
# [*server_tag*]
#   tag used to import ssh_key of server's user. Default: softec_backuppc
#
class softec_backuppc::client (
  $user         = 'softecbackuppc',
  $user_gid     = '3005',
  $user_uid     = '3005',
  $home         = '',
  $server_tag   = 'softec_backuppc',
) {

  $real_home = $home? {
    ''      => "/var/lib/${user}",
    default => $home
  }

  class {'backuppc::client':
    user          => $user,
    user_uid      => $user_uid,
    user_gid      => $user_gid,
    home          => $real_home,
    server_tag    => $server_tag,
    use_ssh_auth  => true,
  }

  #Fx - file civetta #1137
  file {'/etc/restoretest':
    ensure  => present,
    owner   => 'root',
    group   => 'root',
    mode    => '644',
    content => "#Generated by puppet\nripristino backuppc",
  }

  Softec_sudo::Alias {
    target  => '/etc/sudoers.d/backuppc'
  }

  Softec_sudo::Spec {
    target  => '/etc/sudoers.d/backuppc'
  }

  softec_sudo::alias {'BACKUPPC':
    sudo_alias  => 'Host_Alias',
    items       => $::fqdn,
  }

  softec_sudo::alias {'CMD_TAR_CREATE_BACKUPPC':
    sudo_alias  => 'Cmnd_Alias',
    items       => '/usr/bin/nice -n19 /usr/bin/ionice -c2 -n7 /bin/tar -c *',
  }

  softec_sudo::alias {'CMD_TAR_RESTORE_BACKUPPC':
    sudo_alias  => 'Cmnd_Alias',
    items       => '/usr/bin/nice -n19 /usr/bin/ionice -c2 -n7 /bin/tar -x *',
  }

  softec_sudo::alias {'CMD_RSYNC_BACKUPPC':
    sudo_alias  => 'Cmnd_Alias',
    items       => '/usr/bin/nice -n19 /usr/bin/ionice -c2 -n7 /usr/bin/rsync',
  }

  softec_sudo::alias {'TAR_CREATE_BACKUPPC':
    sudo_alias  => 'Cmnd_Alias',
    items       => '/bin/tar -c *',
  }

  softec_sudo::alias {'TAR_RESTORE_BACKUPPC':
    sudo_alias  => 'Cmnd_Alias',
    items       => '/bin/tar -x *',
  }

  softec_sudo::spec {'backuppc':
    users     => ['softecbackuppc', 'backuppc' ],
    hosts     => 'BACKUPPC',
    commands  => ['(root) NOPASSWD:CMD_TAR_CREATE_BACKUPPC', 'CMD_TAR_RESTORE_BACKUPPC', 'CMD_RSYNC_BACKUPPC', 'TAR_CREATE_BACKUPPC', 'TAR_RESTORE_BACKUPPC'],
  }
}
